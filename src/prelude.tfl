; The Trifle prelude. These are functions and macros that are included
; in every namespace. Functions are written without error checking, as
; tracebacks should be clear enough. Macros are written more
; defensively.

; todo: we should document whether functions are built-in or in the
; prelude.
; todo: the docs should have links to the source code where the
; function/macro was defined.
; todo: docstrings

; todo: macros should check argument types to aid debugging
(macro set! (symbol value)
  (quote
    (set-symbol! (quote (unquote symbol)) (unquote value))
  )
)

; todo: macros should check argument types to aid debugging
; todoc
; todo: unit test
(macro function (name params :rest body)
  (quote
    (set! (unquote name)
      (lambda (unquote params) (unquote* body))
    )
  )
)

(function list (:rest items) items)

(function inc (x) (+ x 1))

(macro inc! (x)
  (quote (set! (unquote x) (inc (unquote x))))
)

; todo: dec!
(function dec (x) (- x 1))

(macro for-each (name some-list :rest body)
  (let (index-var (fresh-symbol))
    (quote
      (let ((unquote index-var) 0)
        (while (< (unquote index-var) (length (unquote some-list)))
          (let ((unquote name) (get-index (unquote some-list) (unquote index-var)))
            (unquote* body)
          )
          (inc! (unquote index-var))
        )
      )
    )
  )
)

(function map (func some-list)
  (let (result (list))
    (for-each item some-list
      (append! result (func item))
    )
    result
  )
)

(function first (list)
  (get-index list 0)
)
(function second (list)
  (get-index list 1)
)
(function third (list)
  (get-index list 2)
)
(function fourth (list)
  (get-index list 3)
)
(function fifth (list)
  (get-index list 4)
)

; todoc, especially how it handles empty lists
(function last (lst)
  (get-index lst -1)
)

(function do (:rest args)
  (if args
    (last args)
  )
)

(function not (value)
  (if value false true)
)
